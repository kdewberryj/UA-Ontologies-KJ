@dc:creator "SIE Disruption Lab"
@dc:contributor "Joe Gregory"
@dc:description "A vocabulary to capture decision concepts required for TEMP"
@dc:hasVersion "0.1"
vocabulary <http://siedepartment.com/DoD_TEMP_Framework/TEMP_Decision#> as temp_dec {
	
	////////////////////////////////////////////////////////////////////////////////////
	
	/////// Vocabulary Imports ///////
	
	
	// Import standard vocabularies	
	
    extends <http://purl.org/dc/elements/1.1/> as dc
    extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
    //extends <http://www.w3.org/2001/XMLSchema#> as xsd
    
    // Import relevant vocabularies
    
    extends <http://siedepartment.com/SIE_Foundation/SIE_Foundation#> as foundation
    
    extends <http://siedepartment.com/SIE_Core/SIE_Information#> as info
    extends <http://siedepartment.com/SIE_Core/SIE_Event#> as event
    extends <http://siedepartment.com/SIE_Core/SIE_Agent#> as agent
    //extends <http://siedepartment.com/SIE_Core/SIE_Measurement#> as meas
    //extends <http://siedepartment.com/SIE_Core/SIE_Provenance#> as prov
    
    extends <http://siedepartment.com/SIE_Domain/SIE_SystemArchitecture#> as sa
	//extends <http://siedepartment.com/SIE_Domain/SIE_Mission#> as mission
	extends <http://siedepartment.com/SIE_Domain/SIE_Requirements#> as req
	//extends <http://siedepartment.com/SIE_Domain/SIE_Test#> as test
	//extends <http://siedepartment.com/SIE_Domain/SIE_Software#> as sw
	//extends <http://siedepartment.com/SIE_Domain/SIE_DataManagement#> as dm
	//extends <http://siedepartment.com/SIE_Domain/SIE_ModelingAndSimulation#> as ms
	//extends <http://siedepartment.com/SIE_Domain/SIE_ProjectManagement#> as pm
	//extends <http://siedepartment.com/SIE_Domain/SIE_Acquisition#> as acq
    
    extends <http://siedepartment.com/DoD_TEMP_Framework/TEMP_Organization#> as temp_org
	extends <http://siedepartment.com/DoD_TEMP_Framework/TEMP_TestProgramPolicy#> as temp_tpp
	extends <http://siedepartment.com/DoD_TEMP_Framework/TEMP_TestProgramStructure#> as temp_tps
	//extends <http://siedepartment.com/DoD_TEMP_Framework/TEMP_TestProgramFrameworks#> as temp_tpf
	//extends <http://siedepartment.com/DoD_TEMP_Framework/TEMP_Responsibility#> as temp_resp
	extends <http://siedepartment.com/DoD_TEMP_Framework/TEMP_TestProgramTeams#> as temp_tpt
	
	///////////////////////////////////////////////////////////////////////////////////
	
	/////// Vocabulary Definitions ///////
	
	
	///// Aspects

	@rdfs:label "Data Requirement Identifier"
	@dc:description "A [=DataRequirementIdentifier=] is a..."
	aspect DataRequirementIdentifier
	
	
			
	///// Concepts
	
	@rdfs:label "Make MDA Decision"
	@dc:description "A [=MakeMDADecision=] is a..."
	concept MakeMDADecision :> foundation:Function [
		restricts all relation foundation:specificallyDependentOn to temp_org:MilestoneDecisionAuthority
		restricts some relation foundation:specificallyDependentOn to temp_org:MilestoneDecisionAuthority
		restricts some relation sa:canOutput to KeyDecision
	]
	
	@rdfs:label "MDA Decision Made"
	@dc:description "A [=MDADecisionMade=] is a..."
	concept MDADecisionMade :> sa:FunctionOccurrence [
		restricts all relation event:isManifestationOf to MakeMDADecision
		restricts some relation event:isManifestationOf to MakeMDADecision
		restricts some relation agent:hasOutput to KeyDecision 
	]
	
	@rdfs:label "Make PM Decision"
	@dc:description "A [=MakePMDecision=] is a..."
	concept MakePMDecision :> foundation:Function [
		restricts all relation foundation:specificallyDependentOn to temp_tps:ProgramManager
		restricts some relation foundation:specificallyDependentOn to temp_tps:ProgramManager
		restricts some relation sa:canOutput to KeyDecision
	]
	
	@rdfs:label "PM Decision Made"
	@dc:description "A [=PMDecisionMade=] is a..."
	concept PMDecisionMade :> sa:FunctionOccurrence [
		restricts all relation event:isManifestationOf to MakePMDecision
		restricts some relation event:isManifestationOf to MakePMDecision
		restricts some relation agent:hasOutput to KeyDecision 
	]
	
	@rdfs:label "Key Decision"
	@dc:description "A [=KeyDecision=] is a..."
	concept KeyDecision :> info:PrescriptiveInformationEntity [
		restricts some relation info:prescribes to KeyDecisionPoint
	]
	
	@rdfs:label "KeyDecisionPoint"
	@dc:description "An [=KeyDecisionPoint=] is a..."
	concept KeyDecisionPoint :> foundation:Process [
		restricts all relation foundation:contains to KeyDecisionPoint
	]
	
	@rdfs:label "Milestone"
	@dc:description "An [=Milestone=] is a..."
	concept Milestone :> KeyDecisionPoint
	
	@rdfs:label "Technical Readiness Decision"
	@dc:description "An [=TechnicalReadinessDecision=] is a..."
	concept TechnicalReadinessDecision :> KeyDecisionPoint 
	
	@rdfs:label "Key Integration Point"
	@dc:description "An [=KeyIntegrationPoint=] is a..."
	concept KeyIntegrationPoint :> KeyDecisionPoint 
	
	@rdfs:label "Recommended Corrective Action"
	@dc:description "An [=RecommendedCorrectiveAction=] is a..."
	concept RecommendedCorrectiveAction :> info:PrescriptiveInformationEntity [
		restricts some relation info:prescribes to temp_tpp:TestAndEvaluationProgram
		restricts all relation info:prescribes to temp_tpp:TestAndEvaluationProgram
		restricts some relation foundation:isContainedIn to temp_tpt:IDSK
	]
	
	@rdfs:label "Risk Assessment"
	@dc:description "An [=RiskAssessment=] is a..."
	concept RiskAssessment :> info:DescriptiveInformationEntity [
		restricts some relation info:describes to temp_tpp:TestAndEvaluationProgram
		restricts all relation info:describes to temp_tpp:TestAndEvaluationProgram
		restricts some relation foundation:isContainedIn to temp_tpt:IDSK
	]
	
	@rdfs:label "Operational Issue"
	@dc:description "An [=OperationalIssue=] is a..."
	concept OperationalIssue :> info:DescriptiveInformationEntity [
		restricts some relation foundation:isContainedIn to temp_tpt:IDSK
	]
	
	@rdfs:label "Data Requirement"
	@dc:description "An [=DataRequirement=] is a..."
	concept DataRequirement :> req:Requirement [
		restricts some relation info:prescribes to temp_tpp:TestAndEvaluationProgram
		restricts all relation info:prescribes to temp_tpp:TestAndEvaluationProgram
		restricts some relation foundation:isContainedIn to temp_tpt:IDSK
		restricts some relation isIdentifiedBy to DataRequirementIdentifier
	]
	
	@rdfs:label "Chief Engineer"
	@dc:description "An [=ChiefEngineer=] is a..."
	concept ChiefEngineer :> foundation:Role, DataRequirementIdentifier
	
	
	
	///// Relation Entities
	
	
	@rdfs:label "IdentifiesDataRequirement"
	@dc:description "[=IdentifiesDataRequirement=] is a "
	relation entity IdentifiesDataRequirement [
		from DataRequirementIdentifier
		to DataRequirement
		@rdfs:label "identifies data requirement"
		forward identifiesDataRequirement
		@rdfs:label "is identified by"
		reverse isIdentifiedBy
		asymmetric
		irreflexive
	]
	
	@rdfs:label "Informs"
	@dc:description "[=Informs=] is a "
	relation entity Informs [
		from info:InformationEntity
		to KeyDecision
		@rdfs:label "informs"
		forward informs
		@rdfs:label "is informed by"
		reverse isInformedBy
		asymmetric
		irreflexive
	]
	
	
	
	///// Scalar Properties
	
	
	
	///// Redefined Concepts
	
	ref concept temp_tps:ProgramManager :> DataRequirementIdentifier
	ref concept temp_tpt:ChiefDevelopmentalTester :> DataRequirementIdentifier
	ref concept temp_org:WIPT :> DataRequirementIdentifier
	ref concept temp_org:OperationalTestAgency :> DataRequirementIdentifier

	
	}